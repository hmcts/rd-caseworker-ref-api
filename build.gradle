buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
  dependencies {
    classpath("net.serenity-bdd:serenity-gradle-plugin:2.3.4")
  }
}

plugins {
    id 'application'
    id 'uk.gov.hmcts.java' version '0.12.12'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id "info.solidsoft.pitest" version '1.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
    id 'org.springframework.boot' version '2.4.9'
    id "org.flywaydb.flyway" version '8.5.2'
    id 'au.com.dius.pact' version '4.1.7' // do not change, otherwise serenity report fails
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'

def versions = [
  lombok             : '1.18.20',
  gradlePitest       : '1.5.1',
  pitest             : '1.7.0',
  reformHealthStarter: '0.0.5',
  reformLogging      : '5.1.9',
  serenity           : '2.0.76',
  sonarPitest        : '0.5',
  springBoot         : '2.4.9',
  springfoxSwagger   : '2.9.2',
  pact_version       : '4.1.7',
  launchDarklySdk    : "5.2.2",
  restAssured        : '4.3.3',
  jackson            : '2.13.1',
  log4j              : '2.17.1'
]

mainClassName = 'uk.gov.hmcts.reform.cwrdapi.CaseWorkerRefApiApplication'
def jarName = 'rd-caseworker-ref-api.jar'

group = 'uk.gov.hmcts.reform.rdcaseworker'
version = '0.0.1'

sourceCompatibility = 11
targetCompatibility = 11

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.00
      }
    }
  }
}

check.dependsOn jacocoTestCoverageVerification

compileJava {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
  options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }

  pactTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/pactTest/java')
    }
    resources.srcDir file('src/pactTest/resources')
  }
}

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
    testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
    testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
  }
}

configurations {
  integrationTestImplementation.extendsFrom testCompile
  integrationTestRuntime.extendsFrom testRuntime
  functionalTestImplementation.extendsFrom testCompile
  functionalTestRuntime.extendsFrom testRuntime
  contractTestImplementation.extendsFrom testCompile
  contractTestRuntimeOnly.extendsFrom testRuntime
  pactTestImplementation.extendsFrom testCompile
  pactTestRuntime.extendsFrom testRuntime
}


pmd {
  toolVersion = '6.12.0'
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSets = [
    'category/java/errorprone.xml',
    'category/java/bestpractices.xml'
  ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  finalizedBy aggregate
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

  systemProperty "scenario", System.getProperty('scenario')

  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  finalizedBy aggregate
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

task contract(type: Test) {
  description = 'Runs the consumer Pact tests'
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include "uk/gov/hmcts/reform/cwrdapi/**"
  systemProperty 'pact.rootDir', "pacts"
}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  include "uk/gov/hmcts/reform/cwrdapi/**"
  systemProperty 'pact.rootDir', "pacts"
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // This is a temporary fix to prevent the nightly build from failing if the Fortify scan detects issues
  ignoreExitValue = true
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:9292'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    version = "1.0"
  }
}

//Provider PACT tasks
task contractTest(type: Test) {
  logger.lifecycle("Runnning contract Tests")
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

task runProviderPactVerification(type: Test) {
  logger.lifecycle("Runs pact Tests")
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
  systemProperty 'pact.provider.version', project.pactVersion
  include "uk/gov/hmcts/reform/cwrdapi/*ProviderTest*"
  exclude "uk/gov/hmcts/reform/cwrdapi/*ConsumerTest*"
}

runProviderPactVerification.dependsOn contractTest

runProviderPactVerification.finalizedBy pactVerify

jacoco {
  toolVersion = "0.8.5"
}

jacocoTestReport {
  executionData(test,integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  junit5PluginVersion = '0.15'
  targetClasses = ['uk.gov.hmcts.reform.*']
  testSourceSets = [sourceSets.test]
  excludedClasses = [
    'uk.gov.hmcts.reform.cwrdapi.config.*',
    'uk.gov.hmcts.reform.cwrdapi.controllers.*',
    'uk.gov.hmcts.reform.cwrdapi.service.impl.CaseWorkerDeleteServiceImpl',
    'uk.gov.hmcts.reform.cwrdapi.service.impl.ValidationServiceFacadeImpl',
    'uk.gov.hmcts.reform.cwrdapi.service.impl.CaseWorkerServiceImpl',
    'uk.gov.hmcts.reform.cwrdapi.CaseWorkerRefApiApplication',
    'uk.gov.hmcts.reform.cwrdapi.servicebus.TopicPublisher'
  ]
  threads = 10
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 85
}


project.tasks['pitest'].group = "Verification"

sonarqube {
  properties {
    property "sonar.projectName", "reform::rd-caseworker-ref-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:reform::rd-caseworker-ref-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/cwrdapi/config/**," +
                "src/main/java/uk/gov/hmcts/reform/cwrdapi/client/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/cwrdapi/domain/**," +
                "src/main/java/uk/gov/hmcts/reform/cwrdapi/controllers/request/**," +
                "src/main/java/uk/gov/hmcts/reform/cwrdapi/CaseWorkerRefApiApplication.java"
  }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {

  implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
  implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.0.2'
  implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.6.1'

  implementation group: 'org.springframework', name: 'spring-aop', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-aspects', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-context', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-context-support', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-expression', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-jcl', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-jdbc', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-orm', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-tx', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-web', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-webmvc', version: '5.3.18'

  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.0.5'
  implementation group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'
  implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: '3.8.0'
  implementation (group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0') {
    exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    exclude group: "org.apache.sling"
  }
  implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'
  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  implementation "com.github.hmcts.java-logging:logging:${versions.reformLogging}"
  implementation "com.github.hmcts.java-logging:logging-appinsights:${versions.reformLogging}"
  implementation 'com.github.hmcts:properties-volume-spring-boot-starter:0.1.0'
  implementation 'com.github.hmcts:service-auth-provider-java-client:4.0.3'

  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
  implementation group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.1'
  implementation group: 'com.fasterxml.jackson', name: 'jackson-bom', version: '2.13.2.20220324', ext: 'pom'

  implementation group: 'javax.inject', name: 'javax.inject', version: '1'
  implementation 'com.github.hmcts:idam-java-client:2.0.1'
  implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
  implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '8.20'
  implementation "io.github.openfeign:feign-httpclient:11.0"
  implementation 'org.springframework.boot:spring-boot-starter-validation'

  implementation group: 'org.flywaydb', name: 'flyway-core', version: '6.5.5'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.3.3'

  implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
  implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: versions.launchDarklySdk
  implementation group:"org.yaml", name: "snakeyaml", version:"1.30"

  implementation group: 'com.azure', name: 'azure-core', version: '1.13.0'
  implementation group: 'com.azure', name: 'azure-messaging-servicebus', version: '7.0.2'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
  implementation group: 'org.springframework', name: 'spring-core', version: '5.3.18'
  implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.18'
  implementation group: 'io.netty', name: 'netty-codec-dns', version: '4.1.72.Final'
  implementation group: 'io.netty', name: 'netty-resolver-dns', version: '4.1.72.Final'
  implementation group: 'io.netty', name: 'netty-resolver-dns-native-macos', version: '4.1.69.Final'
  implementation group: 'io.netty', name: 'netty-buffer', version: '4.1.69.Final'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j', version: versions.log4j
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.log4j
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.10'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
  implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.18'

  implementation('io.netty:netty-codec:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-codec-http:4.1.72.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-common:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-handler:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-resolver:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-transport:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-transport-native-epoll:4.1.74.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-transport-native-kqueue:4.1.73.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-transport-native-unix-common:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-codec-http2:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-codec-socks:4.1.73.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  implementation('io.netty:netty-handler-proxy:4.1.69.Final') {
    because 'In Netty before version 4.1.60.Final there is a vulnerability that enables request smuggling.'
  }
  //Fix for CVE-2021-29425
  implementation 'commons-io:commons-io:2.11.0'
  compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation("org.hamcrest:hamcrest-junit:2.0.0.0") {
    exclude group: "org.hamcrest", module: "hamcrest-core"
    exclude group: "org.hamcrest", module: "hamcrest-library"
  }

    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    testImplementation group: 'io.rest-assured', name: 'rest-assured-common', version: versions.restAssured

    testImplementation group: 'com.h2database', name: 'h2'
    testImplementation ("com.github.tomakehurst:wiremock-jre8:2.32.0") {
      exclude group: 'com.github.jknack'
    }
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.4.6'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.4.6'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

  contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
  contractTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.2")
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')

  testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
  testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.7.0'
  testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'

  testImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: '3.0.2'
  testImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: '3.2.0'
  testImplementation 'io.github.openfeign:feign-jackson:10.7.0'
  testImplementation group: 'com.github.mifmif', name: 'generex', version: '1.0.2'

  testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:3.1.0'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  testImplementation('com.opentable.components:otj-pg-embedded:0.13.4')
  testImplementation group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.14'
  testImplementation group: 'org.codehaus.groovy', name: 'groovy-xml', version: '2.5.14'
  testImplementation group: 'org.codehaus.groovy', name: 'groovy-json', version: '2.5.14'
  testImplementation group: 'org.springframework', name: 'spring-test', version: '5.3.16'
  testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
  testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.10'


  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
  //Fix for CVE-2021-35515, CVE-2021-35516, CVE-2021-35517, CVE-2021-36090
  implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'
  testImplementation 'com.github.hmcts:fortify-client:1.2.0:all'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.1.7'
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: '4.1.7'
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.1.7'
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: '4.1.7'
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5', version: '4.1.7'
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'spring', version: '4.1.7'
  contractTestImplementation group: 'au.com.dius.pact.provider', name: 'junit5spring', version: '4.1.7'

  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  pactTestImplementation sourceSets.main.runtimeClasspath
  pactTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {

  dependencies {
    // CVE-2021-42340
    dependencySet(group: 'org.apache.tomcat.embed', version: '9.0.58') {
      entry 'tomcat-embed-core'
      entry 'tomcat-embed-el'
      entry 'tomcat-embed-websocket'
    }
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

gradle.startParameter.continueOnFailure = true

bootJar {
  archiveFileName = jarName
  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}
